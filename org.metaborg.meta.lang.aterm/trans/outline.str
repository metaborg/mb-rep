module outline

imports

  signatures/ATerm-sig
  libspoofax/editor/outline

rules
  
  editor-outline:
    (node, position, ast, path, project-path) -> outline
    where
      outline := <outline> ast
  
  outline = custom-label-outline(to-outline-label, to-outline-node)

  // heuristic: expand to level 3 if the second level has less than four nodes.
  // Otherwise, expand to level 2.
  outline-expand-to-level =
    ?[Node(_, children)]; !(<length> children, 4); lt; !3 
      <+
    !2

rules  

  to-outline-label: Appl(fun, args) -> fun{"icons/application.gif"}
  to-outline-label: Tuple(args)     -> "( )"{"icons/tuple.gif"}
  to-outline-label: List(args)      -> "[ ]"{"icons/list.gif"}
  to-outline-label: Int(value)      -> value{"icons/int.png"}
  to-outline-label: Real(value)     -> value{"icons/int.png"}
  to-outline-label: Fun(value)      -> value{"icons/string.png"}

  to-outline-node:  Annotated(trm, _) -> <outline; Hd> trm
